buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle.forge"
sourceCompatibility = targetCompatibility = "1.8"

configurations {
    shade
    compile.extendsFrom(shade)
}

dependencies {
    shade(project(":ConverterCore")) { transitive(false) }
}

minecraft {
    version = "1.10.2-12.18.3.2511"
    runDir = "run"
    mappings = "snapshot_20161111"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task shadeJar(type: Copy) {
    includeEmptyDirs = false
    from { configurations.shade.collect { zipTree(it) } }
    include("**/*.class")
    destinationDir("$buildDir/classes/java/main" as File)
}

sourceJar {
    enabled(false)
}

sourceApiJava {
    enabled(false)
}

jar {
    dependsOn(shadeJar)
}